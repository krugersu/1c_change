// Функция формирует структуру с параметрами для вызова обработки.
//
&НаСервере
Функция ПодготовитьСтруктуруДанных(ПараметрКоманды)
		
	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("Магазин", ПараметрКоманды.Магазин);
	СтруктураПараметры.Вставить("Склад",   ПараметрКоманды.Склад);
	СтруктураПараметры.Вставить("Документ",ПараметрКоманды);
	СтруктураПараметры.Вставить("Режим",   "ПриходТовара");
	
	Возврат СтруктураПараметры;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	МассивДокументов = Новый Массив;
	МассивДокументов.Добавить(ПараметрКоманды);
	МассивНепроведенныхДокументов = ФормированиеПечатныхФормВызовСервера.ПолучитьМассивНепроведенныхДокументов(МассивДокументов);
	
	Если МассивНепроведенныхДокументов.Количество() > 0 Тогда
		
		ТекстВопроса = НСтр("ru = 'Распределение товаров по складам возможно
			|только после проведения документа, провести документ?'");
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("МассивНепроведенныхДокументов", МассивНепроведенныхДокументов);
		ДополнительныеПараметры.Вставить("ПараметрКоманды", ПараметрКоманды);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПровестиНепроведенныеДокументы", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		ОткрытьФормуРаспределенияТоваров(ПараметрКоманды, МассивНепроведенныхДокументов)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРаспределенияТоваров(ПараметрКоманды, МассивНепроведенныхДокументов)
	
	Если МассивНепроведенныхДокументов.Количество() = 0 Тогда
		СтруктураДанных = ПодготовитьСтруктуруДанных(ПараметрКоманды);
		ОткрытьФорму("Обработка.РаспределениеТоваровПоСкладам.Форма.Форма", СтруктураДанных,, Новый УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиНепроведенныеДокументы(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		МассивНепроведенныхДокументов = ФормированиеПечатныхФормВызовСервера.ПровестиДокументы(ДополнительныеПараметры.МассивНепроведенныхДокументов);
		ОткрытьФормуРаспределенияТоваров(ДополнительныеПараметры.ПараметрКоманды, МассивНепроведенныхДокументов);
	КонецЕсли;
	
КонецПроцедуры

